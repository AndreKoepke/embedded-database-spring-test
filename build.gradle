
plugins {
    id 'net.researchgate.release' version '2.6.0'
}

// it's a workaround for https://github.com/researchgate/gradle-release/issues/186
task build(dependsOn: [':embedded-database-spring-test-core:build', ':embedded-database-spring-test-main:build'])

allprojects {
    group 'io.zonky'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.assertj:assertj-core:3.6.1'
        testCompile 'ch.qos.logback:logback-classic:1.2.3'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    test {
        useJUnit()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
            all {
                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each { it.scope*.value = 'compile'}
                }
            }
        }
    }

    release {
        git {
            requireBranch = '^(master|\\d+\\.\\d+\\.x)$'
        }
    }
}

project(':embedded-database-spring-test-core') {
    dependencies {
        compile 'org.springframework:spring-context:4.3.10.RELEASE'
        compile 'org.springframework:spring-test:4.3.10.RELEASE'
        compile 'com.opentable.components:otj-pg-embedded:0.9.0'
        compile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:4.2.0.1'
        compile 'com.google.guava:guava:23.0'
    }
}

project(':embedded-database-spring-test-main') {
    dependencies {
        compile project(':embedded-database-spring-test-core')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'embedded-database-spring-test'
            }
        }
    }
}
