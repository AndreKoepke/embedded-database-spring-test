import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import java.util.regex.Matcher

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

ext {
    ossrhUsername = project.findProperty('ossrh.username')
    ossrhPassword = project.findProperty('ossrh.password')

    flywayCoreVersions = ['3.0', '3.1', '3.2.1', '4.0.3', '4.1.2']
    flywayTestExtensionsVersions = ['3.0.1', '3.1', '3.2.1.1', '4.0.1', '4.1.0']
}

allprojects {
    group 'io.zonky.test'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

    configurations {
        included
        archives.extendsFrom included

        for (version in flywayCoreVersions) {
            "testRuntimeWithFlywayCore$version" {
                extendsFrom testRuntime
                resolutionStrategy {
                    eachDependency { details ->
                        if (details.requested.name == 'flyway-core') {
                            details.useVersion "$version"
                        }
                    }
                }
            }
        }

        for (version in flywayTestExtensionsVersions) {
            "testRuntimeWithFlywayTestExtensions$version" {
                extendsFrom testRuntime
                resolutionStrategy {
                    eachDependency { details ->
                        if (details.requested.group == 'org.flywaydb.flyway-test-extensions') {
                            details.useVersion "$version"
                        }
                    }
                }
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.assertj:assertj-core:3.6.1'
        testCompile 'ch.qos.logback:logback-classic:1.2.3'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    test {
        useJUnit()
    }

    for (version in flywayCoreVersions) {
        task "testWithFlywayCore$version"(type: Test) {
            dependsOn jar

            classpath -= configurations.testRuntime
            classpath += configurations."testRuntimeWithFlywayCore$version"

            useJUnit {
                includeCategories 'io.zonky.test.category.FlywayIntegrationTests'
                includeCategories 'io.zonky.test.category.MultiFlywayIntegrationTests'
            }
        }
        check.dependsOn("testWithFlywayCore$version")
    }

    for (version in flywayTestExtensionsVersions) {
        task "testWithFlywayTestExtensions$version"(type: Test) {
            dependsOn jar

            classpath -= configurations.testRuntime
            classpath += configurations."testRuntimeWithFlywayTestExtensions$version"

            useJUnit {
                includeCategories 'io.zonky.test.category.FlywayIntegrationTests'

                if (VersionNumber.withPatchNumber().parse(version) >= VersionNumber.withPatchNumber().parse("4.2.0.1")) {
                    println "MultiFlywayIntegrationTests category has been applied!!! - $version"
                    includeCategories 'io.zonky.test.category.MultiFlywayIntegrationTests'
                }
            }
        }
        check.dependsOn("testWithFlywayTestExtensions$version")
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'embedded-database-spring-test'
                    packaging 'jar'
                    description 'A library for creating isolated embedded databases for Spring-powered integration tests.'
                    url 'https://github.com/zonkyio/embedded-database-spring-test'

                    scm {
                        connection 'scm:git:git://github.com/zonkyio/embedded-database-spring-test.git'
                        developerConnection 'scm:git:ssh://github.com:zonkyio/embedded-database-spring-test.git'
                        url 'https://github.com/zonkyio/embedded-database-spring-test/tree/master'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name 'Roman Pichlik'
                            email 'roman.pichlik@zonky.cz'
                        }
                        developer {
                            name 'Tomas Vanek'
                            email 'tomas.vanek@zonky.cz'
                        }
                        developer {
                            name 'Developers Zonky'
                            email 'developers@zonky.cz'
                        }
                    }
                }
            }
        }
    }

    release {
        tagTemplate = 'v${version}'
        versionPatterns = [
            // increments the minor version: "1.0.0-SNAPSHOT" => "1.1.0-SNAPSHOT"
            /(\d+)\.\d+([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}.0${m[0][2]}") }
        ]
        git {
            requireBranch = '^(master|\\d+\\.\\d+\\.x)$'
        }
    }
}

project(':embedded-database-spring-test-autoconfigure') {
    archivesBaseName = 'embedded-database-spring-test'

    dependencies {
        compile project(':embedded-database-spring-test-core')
    }
}

project(':embedded-database-spring-test-core') {
    dependencies {
        runtime 'io.zonky.test.postgres:embedded-postgres-binaries-windows-amd64:10.4.0'
        runtime 'io.zonky.test.postgres:embedded-postgres-binaries-darwin-amd64:10.4.0'
        runtime 'io.zonky.test.postgres:embedded-postgres-binaries-linux-amd64:10.4.0'

        compile 'org.springframework:spring-context:4.3.10.RELEASE'
        compile 'org.springframework:spring-test:4.3.10.RELEASE'
        compile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:4.2.0.2'
        compile 'com.google.guava:guava:23.0'

        // some classes of otj-pg-embedded component are repackaged into the shadowed jar
        included 'com.opentable.components:otj-pg-embedded:0.9.0'

        configurations.included.incoming.resolutionResult.allDependencies { result ->
            compile result.getRequested().getDisplayName()
        }
    }

    def installer = install.repositories.mavenInstaller
    def deployer = uploadArchives.repositories.mavenDeployer

    [installer, deployer]*.pom*.whenConfigured { pom ->
        pom.dependencies = pom.dependencies.findAll {
            it.groupId != 'com.opentable.components' || it.artifactId != 'otj-pg-embedded'
        }
    }

    task shadowJar(group: 'build', type: ShadowJar) {
        from sourceSets.main.output
        configurations = [project.configurations.included]

        dependencies {
            exclude(dependency {
                it.moduleGroup != 'com.opentable.components' || it.moduleName != 'otj-pg-embedded'
            })
        }

        exclude 'com/opentable/db/postgres/embedded/BundledPostgresBinaryResolver.class'
        exclude 'com/opentable/db/postgres/junit/**'
        exclude '.repacked'
        exclude '*.txz'

        relocate 'com.opentable.db.postgres.embedded', 'io.zonky.test.db.postgres.embedded'
    }

    jar {
        enabled = false
        dependsOn shadowJar
    }
}

task updateReadmeVersion() {
    doLast {
        def version = project.version.toString()
        ant.replaceregexp(file: 'README.md', match: '<version>\\d+\\.\\d+\\.\\d+</version>', replace: "<version>$version</version>", flags: 'g', byline: true)
    }
}

// workaround for https://github.com/researchgate/gradle-release/issues/186
task build(group: 'build') {
    subprojects.each {
        dependsOn "${it.path}:build"
    }
}

runBuildTasks.dependsOn updateReadmeVersion

afterReleaseBuild {
    subprojects.each {
        dependsOn "${it.path}:uploadArchives"
    }
}
