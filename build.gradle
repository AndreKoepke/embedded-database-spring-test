import java.util.regex.Matcher

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'nebula.optional-base' version '3.3.0'
}

ext {
    ossrhUsername = project.findProperty('ossrh.username')
    ossrhPassword = project.findProperty('ossrh.password')

    versions = [
            [name: 'flyway-3-0',   flyway: '3.0',   'flyway-test': '3.0.1',   liquibase: '3.5.3', spring: '4.3.8.RELEASE',  'spring-boot': '1.4.6.RELEASE'],
            [name: 'flyway-3-1',   flyway: '3.1',   'flyway-test': '3.1',     liquibase: '3.5.3', spring: '4.3.8.RELEASE',  'spring-boot': '1.4.6.RELEASE'],
            [name: 'flyway-3-2-1', flyway: '3.2.1', 'flyway-test': '3.2.1.1', liquibase: '3.5.3', spring: '4.3.8.RELEASE',  'spring-boot': '1.4.6.RELEASE'],
            [name: 'flyway-4-0-3', flyway: '4.0.3', 'flyway-test': '4.0.1',   liquibase: '3.5.5', spring: '4.3.25.RELEASE', 'spring-boot': '1.5.22.RELEASE'],
            [name: 'flyway-4-1-2', flyway: '4.1.2', 'flyway-test': '4.1.0',   liquibase: '3.5.5', spring: '4.3.25.RELEASE', 'spring-boot': '1.5.22.RELEASE'],
            [name: 'flyway-4-2-0', flyway: '4.2.0', 'flyway-test': '4.2.0.2', liquibase: '3.5.5', spring: '4.3.25.RELEASE', 'spring-boot': '1.5.22.RELEASE'],
            [name: 'flyway-5-1-4', flyway: '5.1.4', 'flyway-test': '5.1.0',   liquibase: '3.5.5', spring: '5.0.13.RELEASE', 'spring-boot': '2.0.9.RELEASE'],
            [name: 'flyway-5-2-4', flyway: '5.2.4', 'flyway-test': '5.2.4',   liquibase: '3.6.3', spring: '5.1.14.RELEASE', 'spring-boot': '2.1.13.RELEASE'],
            [name: 'flyway-6-0-2', flyway: '6.0.2', 'flyway-test': '6.0.0',   liquibase: '3.8.8', spring: '5.2.5.RELEASE',  'spring-boot': '2.2.6.RELEASE'],
            [name: 'flyway-6-0-7', flyway: '6.0.7', 'flyway-test': '6.0.0',   liquibase: '3.8.8', spring: '5.2.5.RELEASE',  'spring-boot': '2.2.6.RELEASE'],
            [name: 'flyway-none',  flyway: 'none',  'flyway-test': 'none',    liquibase: 'none' , spring: '4.3.25.RELEASE', 'spring-boot': '1.5.22.RELEASE'],
            [name: 'default',      flyway: '5.0.7', 'flyway-test': '5.0.0',   liquibase: '3.5.5', spring: '4.3.25.RELEASE', 'spring-boot': '1.5.22.RELEASE'] // default versions
    ]

    postgresVersions = ['9.4.25', '9.5.20', '9.6.16', '10.11.0', '11.6.0', '12.1.0']
    defaultVersions = versions.find { it.name == 'default' }

    flywayCoreVersion = defaultVersions['flyway']
    flywayTestVersion = defaultVersions['flyway-test']
    liquibaseCoreVersion = defaultVersions['liquibase']
    springCoreVersion = defaultVersions['spring']
    springBootVersion = defaultVersions['spring-boot']
}

allprojects {
    group 'io.zonky.test'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'nebula.optional-base'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'embedded-database-spring-test'
                    packaging 'jar'
                    description 'A library for creating isolated embedded databases for Spring-powered integration tests.'
                    url 'https://github.com/zonkyio/embedded-database-spring-test'

                    scm {
                        connection 'scm:git:git://github.com/zonkyio/embedded-database-spring-test.git'
                        developerConnection 'scm:git:ssh://github.com:zonkyio/embedded-database-spring-test.git'
                        url 'https://github.com/zonkyio/embedded-database-spring-test/tree/master'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name 'Roman Pichlik'
                            email 'roman.pichlik@zonky.cz'
                        }
                        developer {
                            name 'Tomas Vanek'
                            email 'tomas.vanek@zonky.cz'
                        }
                        developer {
                            name 'Developers Zonky'
                            email 'developers@zonky.cz'
                        }
                    }
                }
            }
        }
    }

    release {
        tagTemplate = 'v${version}'
        versionPatterns = [
            // increments the minor version: "1.0.0-SNAPSHOT" => "1.1.0-SNAPSHOT"
            /(\d+)\.\d+([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}.0${m[0][2]}") }
        ]
        git {
            requireBranch = '^(master|\\d+\\.\\d+\\.x)$'
        }
    }
}

project(':embedded-database-spring-test-autoconfigure') {
}

project(':embedded-database-spring-test') {
    dependencies {
        compile project(':embedded-database-spring-test-autoconfigure')

        compile 'org.testcontainers:postgresql:1.14.1'
        compile "org.testcontainers:mssqlserver:1.14.1"
        compile 'org.testcontainers:mysql:1.14.1'
        compile 'io.zonky.test:embedded-postgres:1.2.6', optional
        compile 'com.opentable.components:otj-pg-embedded:0.13.1', optional
        compile 'ru.yandex.qatools.embed:postgresql-embedded:2.10', optional
        compile 'com.microsoft.sqlserver:mssql-jdbc:8.2.2.jre8', optional
        compile 'mysql:mysql-connector-java:8.0.15', optional

        compile "org.flywaydb:flyway-core:$flywayCoreVersion", optional
        compile "org.flywaydb.flyway-test-extensions:flyway-spring-test:$flywayTestVersion", optional
        compile "org.springframework.boot:spring-boot-starter-test:$springBootVersion", optional
        compile "org.liquibase:liquibase-core:$liquibaseCoreVersion", optional

        compile "org.springframework:spring-context:$springCoreVersion"
        compile "org.springframework:spring-test:$springCoreVersion"
        compile 'com.google.guava:guava:23.0'
        compile 'org.tukaani:xz:1.8'

        compile('com.cedarsoftware:java-util:1.34.0') {
            exclude group: 'org.apache.logging.log4j'
        }

        testCompile "org.springframework:spring-jdbc:$springCoreVersion"
        testCompile 'ch.qos.logback:logback-classic:1.2.3'
        testCompile("org.assertj:assertj-core:3.14.0")
    }

    configurations {
        versions.init().each { version ->
            "testRuntime_${version.name}" {
                extendsFrom testRuntime
                resolutionStrategy {

                    if (version.flyway == 'none') {
                        exclude group: 'org.flywaydb', module: 'flyway-core'
                    } else {
                        eachDependency { details ->
                            if (details.requested.name == 'flyway-core') {
                                details.useVersion "${version.flyway}"
                            }
                        }
                    }

                    if (version['flyway-test'] == 'none') {
                        exclude group: 'org.flywaydb.flyway-test-extensions', module: 'flyway-spring-test'
                    } else {
                        eachDependency { details ->
                            if (details.requested.group == 'org.flywaydb.flyway-test-extensions') {
                                details.useVersion "${version['flyway-test']}"
                            }
                        }
                    }

                    if (version.liquibase == 'none') {
                        exclude group: 'org.liquibase', module: 'liquibase-core'
                    } else {
                        eachDependency { details ->
                            if (details.requested.name == 'liquibase-core') {
                                details.useVersion "${version.liquibase}"
                            }
                        }
                    }

                    eachDependency { details ->
                        if (details.requested.group == 'org.springframework') {
                            details.useVersion "${version.spring}"
                        }
                    }

                    eachDependency { details ->
                        if (details.requested.group == 'org.springframework.boot') {
                            details.useVersion "${version['spring-boot']}"
                        }
                    }
                }
            }
        }

        postgresVersions.each { version ->
            def versionName = version.replaceAll('\\.', '-')
            "testRuntime_postgres-$versionName" {
                extendsFrom testRuntime
                resolutionStrategy {
                    eachDependency { details ->
                        if (details.requested.group == 'io.zonky.test.postgres') {
                            details.useVersion "$version"
                        }
                    }
                }
            }
        }
    }

    versions.init().each { version ->
        task "test_${version.name}"(type: Test, group: 'verification') {
            dependsOn jar

            classpath -= configurations.testRuntime
            classpath += configurations."testRuntime_${version.name}"

            useJUnit {
                excludeCategories 'io.zonky.test.category.StaticTests'

                if (version.flyway == 'none') {
                    excludeCategories 'io.zonky.test.category.FlywayTests'
                    excludeCategories 'io.zonky.test.category.MultiFlywayTests'
                }

                if (version['flyway-test'] == 'none') {
                    excludeCategories 'io.zonky.test.category.FlywayTests'
                    excludeCategories 'io.zonky.test.category.MultiFlywayTests'
                } else if (VersionNumber.withPatchNumber().parse(version.test) < VersionNumber.withPatchNumber().parse("4.2.0.1")) {
                    excludeCategories 'io.zonky.test.category.MultiFlywayTests'
                }

                if (version.liquibase == 'none') {
                    excludeCategories 'io.zonky.test.category.LiquibaseTests'
                }
            }
        }
        check.dependsOn("test_${version.name}")
    }

    postgresVersions.each { version ->
        def versionName = version.replaceAll('\\.', '-')
        task "test_postgres-$versionName"(type: Test, group: 'verification') {
            dependsOn jar

            classpath -= configurations.testRuntime
            classpath += configurations."testRuntime_postgres-$versionName"
        }
        check.dependsOn("test_postgres-$versionName")
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            showStackTraces = true
            exceptionFormat = 'full'
        }
    }
}

task updateReadmeVersion() {
    doLast {
        def version = project.version.toString()
        ant.replaceregexp(file: 'README.md',
                match: '(?<=<artifactId>embedded-database-spring-test</artifactId>\\n\\s{4,12})<version>\\d+\\.\\d+\\.\\d+</version>',
                replace: "<version>$version</version>", flags: 'g')
    }
}

// workaround for https://github.com/researchgate/gradle-release/issues/186
task build(group: 'build') {
    subprojects.each {
        dependsOn "${it.path}:build"
    }
}

runBuildTasks.dependsOn updateReadmeVersion

afterReleaseBuild {
    subprojects.each {
        dependsOn "${it.path}:uploadArchives"
    }
}
